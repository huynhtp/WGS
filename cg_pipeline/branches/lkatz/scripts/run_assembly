#!/usr/bin/env perl

# run-assembly: Perform standard assembly protocol operations on 454 SFFs, Ion Torrent SFFs, and/or Illumina fastqs
# Author: Andrey Kislyuk (kislyuk@gatech.edu)
# Author: Lee Katz (lkatz@cdc.gov)

package PipelineRunner;
my ($VERSION) = ('$Id: $' =~ /,v\s+(\d+\S+)/o);

my $settings = {
	appname => 'cgpipeline',
};
my $stats;

use strict;
use FindBin;
use lib "$FindBin::RealBin/../lib";
$ENV{PATH} = "$FindBin::RealBin:".$ENV{PATH};
use AKUtils qw(logmsg);

use Getopt::Long;
use File::Temp ('tempdir');
use File::Path;
use File::Spec;
use File::Copy;
use File::Basename;
use List::Util qw(min max sum shuffle);
use CGPipelineUtils;
use Data::Dumper;

$0 = fileparse($0);
local $SIG{'__DIE__'} = sub { my $e = $_[0]; $e =~ s/(at [^\s]+? line \d+\.$)/\nStopped $1/; die("$0: ".(caller(1))[3].": ".$e); };
sub logmsg {my $FH = $FSFind::LOG || *STDOUT; print $FH "$0: ".(caller(1))[3].": @_\n";}

exit(main());

sub main() {
	$settings = AKUtils::loadConfig($settings);

	die usage() if @ARGV < 1;

	my @cmd_options = qw(Reference=s@ keep tempdir=s output=s debug numcpus=i estimatedGenomeSize=i illumina=s@ 454=s@);
	GetOptions($settings, @cmd_options) or die;
	$$settings{numcpus}||=1;

	$$settings{assembly_min_contig_length} ||= 500;
	$$settings{outfile} = $$settings{output};
	$$settings{outfile} ||= "$0.out.fasta";
	$$settings{outfile} = File::Spec->rel2abs($$settings{outfile});
	open(FH, '>', $$settings{outfile}) or die("Error writing to output file $$settings{outfile}");
	close FH;
	logmsg "Output file is " . $$settings{outfile};

	my @ref_files = @{$$settings{Reference}} if defined $$settings{Reference};
	logmsg "No reference files supplied. Reverting to assembly mode" unless @ref_files;

	my @input_files = @ARGV;

	$$settings{tempdir} ||= tempdir(File::Spec->tmpdir()."/$0.$$.XXXXX", CLEANUP => !($$settings{keep}));
  mkdir $$settings{tempdir} if(!-d $$settings{tempdir});
	logmsg "Temporary directory is $$settings{tempdir}";

  # TODO add other chemistry type as needed (PacBio, Ion Torrent)
	foreach my $file (@input_files, @ref_files, @{$$settings{illumina}}, @{$$settings{454}}) {
		$file = File::Spec->rel2abs($file);
		die("Input or reference file $file not found") unless -f $file;
	}
  # make sure each specified platform raw read set is marked as such
  for my $platform(qw(illumina 454 pacbio iontorrent)){
    foreach my $file (@{$$settings{$platform}}) {
      $$settings{"is_".$platform}{$file}=1;
      push(@input_files,$file);
    }
  }

  my $fastaqualfiles=baseCall(\@input_files,$settings); #array of [0=>fna,1=>qual file] (array of arrays)

  my ($final_seqs,$command,@assembly);

	if (@ref_files) {
    my @all_ref_assembly=(); # all assemblies from all programs and merges
    for my $ref_file(@ref_files){
      my $ref_name=fileparse($ref_file);
      system("mkdir $$settings{tempdir}/$ref_name") if(!-d "$$settings{tempdir}/$ref_name");
      die "Could not make the target directory because $!" if $?;
      my @ref_assembly=(); # this reference's ref assembly
      my $ref_assembly="$$settings{tempdir}/$ref_name/assembly.fasta"; # final assembly name for this ref
      MAPPING_454:
      if(@{ $$fastaqualfiles{454} }){
        my $newbler_basename=File::Spec->rel2abs("$$settings{tempdir}/$ref_name/454");
        my $out454="$newbler_basename/assembly.fasta";
        push(@ref_assembly,$out454);
        #goto MAPPING_ILLUMINA;
        $command="run_assembly_454.pl ".join(" ",@{ $$fastaqualfiles{sff} }) . " -R $ref_file -t $newbler_basename -o $out454 2>&1";
        logmsg "Running 454 mapping assembly with\n    $command";
        system($command) unless(-s $out454 > 1 || $$settings{debug});
        die "Could not run Newbler" if $?;

        # TODO
        #MAPPING_POLISHING:
        if(@{ $$fastaqualfiles{illumina} }){
          logmsg "TODO polish the reference assembly with Illumina reads, if they exist";
          my $polishing_basename=File::Spec->rel2abs("$$settings{tempdir}/$ref_name/polishing");
          #TODO: map reads to the 454 assembly
          #TODO: samToFasta: keep original base calls when mapping quality isn't good
        #  $out454=$outPolishing;
        }
        push(@ref_assembly, $out454);
      }

      MAPPING_ILLUMINA:
      if(@{ $$fastaqualfiles{illumina} }){
        logmsg "Performing mapping assembly of Illumina";
        my $illumina_basename=File::Spec->rel2abs("$$settings{tempdir}/$ref_name/illumina");
        mkdir($illumina_basename) if(!-d $illumina_basename);
        my $outIllumina="$illumina_basename/assembly.fasta";
        system("run_assembly_illumina.pl ".join(" ",@{ $$fastaqualfiles{illumina} })." -R $ref_file -t $illumina_basename -o $illumina_basename/assembly.fasta --numcpus=$$settings{numcpus} -e estimatedGenomeSize 2>&1");
        die "Could not perform illumina assembly" if $?;
        push(@ref_assembly,$outIllumina);
      }

      # combine Illumina/454
      my $command="run_assembly_combine.pl -a ".join(" -a ",@ref_assembly)." -o $ref_assembly 2>&1";
      logmsg "Combining mapping assemblies for $ref_name";
      system($command);
      push(@ref_assembly,$ref_assembly);
      push(@all_ref_assembly,@ref_assembly);
    }
    # choose the best combined assembly
    my $finalAsm="$$settings{tempdir}/final.fna";
    logmsg "Choosing the best assembly out of all the reference assemblies. Warning: This has not been tested yet.";
    system("run_assembly_chooseBest.pl ".join(" ",@all_ref_assembly)." -o $finalAsm"); die if $?;
    $final_seqs = AKUtils::readMfa($finalAsm);
	} else {
    
    system("mkdir $$settings{tempdir}/DENOVO") if(!-d "$$settings{tempdir}/DENOVO");
    die "Could not make the target directory because $!" if $?;

    DENOVO_454:
    if(@{ $$fastaqualfiles{454} }){
      my $newbler_basename=File::Spec->rel2abs("$$settings{tempdir}/DENOVO/454");
      my $out454="$newbler_basename/assembly.fasta";
      #push(@assembly,$out454); goto DENOVO_ILLUMINA;
      $command="run_assembly_454.pl ".join(" ",@{ $$fastaqualfiles{sff} }) . " -t $newbler_basename -o $newbler_basename/assembly.fasta 2>&1";
      logmsg "Running Newbler assembly with\n    $command";
      system($command) unless($$settings{debug});
      die "Could not run Newbler" if $?;

      # TODO turn polishing into a subroutine.
      # TODO make it a combination b/n smalt and samToFasta
      #DENOVO_POLISHING:
      #if(@{ $$fastaqualfiles{illumina} }){
      #  logmsg "Polishing the 454 assembly with Nesoni";
      #  my $polishing_basename=File::Spec->rel2abs("$$settings{tempdir}/DENOVO/polishing");
      #  my $outPolishing=File::Spec->abs2rel("$polishing_basename/consensus_masked.fa");
      #  #push(@assembly,$out454); goto DENOVO_ILLUMINA;
      #  system("mkdir $polishing_basename") if(!-d $polishing_basename);
      #  system("nesoni samshrimp: $polishing_basename $out454 reads: ".join(" ",@{ $$fastaqualfiles{illumina} })." --sam-unaligned no") unless($$settings{debug});
      #  die "Problem with Nesoni and/or SHRiMP" if $?;
      #  system("nesoni samconsensus: $polishing_basename --majority 0.9 --cutoff 0.9") unless($$settings{debug});
      #  die "Problem with Nesoni and/or Samtools" if $?;
      #  $out454=$outPolishing;
      #}
      push(@assembly, $out454);
    }

    DENOVO_ILLUMINA:
    if(@{ $$fastaqualfiles{illumina} }){
      logmsg "Performing denovo assembly of Illumina";
      my $illumina_basename=File::Spec->rel2abs("$$settings{tempdir}/DENOVO/illumina");
      my $outIllumina="$illumina_basename/assembly.fasta";
      system("mkdir $illumina_basename") if(!-d $illumina_basename);
      die "Could not make the dir $illumina_basename because $!" if $?;
      #push(@assembly,$outIllumina); goto DENOVO_COMBINING;
      $command="run_assembly_illumina.pl ".join(" ",@{ $$fastaqualfiles{illumina} })." -o $outIllumina -t $illumina_basename --numcpus $$settings{numcpus} -e $$settings{estimatedGenomeSize} 2>&1";
      logmsg "Running Illumina assembly with\n   $command";
      system($command) unless($$settings{debug});
      die "Could not perform Velvet assembly" if $?;
      push(@assembly,$outIllumina);
    }

    # sanity check
    die "There are no individual assemblies to combine!" if(@assembly < 1);

    DENOVO_COMBINING:
    # run combining stage between 454 and Illumina assemblies.
    # TODO consider unused reads
    logmsg "Combining individual assemblies: ".join(" ",@assembly);
    my $combined_filename="$$settings{tempdir}/DENOVO/final.fna";
    mkdir("$$settings{tempdir}/DENOVO/combining") if(!-d "$$settings{tempdir}/DENOVO/combining");
    $command="run_assembly_combine.pl -a '".join("' -a '",@assembly)."' -o '$combined_filename' -t '$$settings{tempdir}/DENOVO/combining' -m $$settings{assembly_min_contig_length} ";
    #$command.="-r $$settings{tempdir}/DENOVO/newbler_Singleton.sff.fasta -r $$settings{tempdir}/DENOVO/newbler_Repeat.sff.fasta "; # adding in extra reads doesn't add much
    $command.="2>&1 ";
    logmsg "Combining with command\n   $command";
    system($command) unless($$settings{debug});

		$final_seqs = AKUtils::readMfa("$combined_filename");
	}
	
  # TODO this filtering step is not necessary if run_assembly_combine.pl does it for us.
	foreach my $seq (keys %$final_seqs) {
		delete $$final_seqs{$seq} if length($$final_seqs{$seq}) < $$settings{assembly_min_contig_length};
	}
	AKUtils::printSeqsToFile($final_seqs, $$settings{outfile}, {order_seqs_by_name => 1});

	logmsg "Output is in $$settings{outfile}";

	return 0;
}

# determine file types
sub ext($){ # return the extension of a filename
  my($file)=@_;
  my($name,$path,$ext)=fileparse($file,qw(.fastq.gz .sff .fasta .fna .fa .fas .fastq .ffn .mfa .fq));
  if(!$ext){
    $ext=$file;
    $ext=~s/.+(\..*$)/\1/;
    #warn "Extension of $file is not standard. Using $ext as the extension";
  }
  $ext=~s/^.//; # remove that dot
  #my $ext=$file;
  #$ext=~s/^.+\.//;
  return lc($ext);
}
sub is_sff($;$){
  my($file,$settings)=@_;
  return 1 if($$settings{is_454}{$file} || $$settings{is_iontorrent}{$file} || ext($file) eq 'sff');
  return 0;
}
sub is_fasta($;$){
  my($file,$settings)=@_;
  return 1 if(ext($file)=~/^(fna|fasta|ffn|fa|fas|mfa)$/i);
  return 0;
}
sub is_fastq($;$){
  my($file,$settings)=@_;
  return 1 if($$settings{is_illumina}{$file} || ext($file)=~/^(fastq|fq)$/i);
  return 0;
}
sub is_fastqGz($;$){
  my($file,$settings)=@_;
  return 1 if(ext($file)=~/^(fastq\.gz)$/i);
  return 0;
}

# Wrapper for all base calling for input files
# Separates chemistries by extension
# TODO in the future somehow distinguish between chemistries using user-inputted flags
sub baseCall($$){
  my($input_files,$settings)=@_;
  my $long=[]; my $short=[]; my $sff=[];
  foreach my $file (@$input_files){
    my $seqQualPair; # [0=>seq,1=>qual]
    if($$settings{is_454}{$file} || $$settings{is_iontorrent}{$file} || is_sff($file,$settings)){
      $seqQualPair=sff2fastaqual([$file], $settings);
      $seqQualPair=$$seqQualPair[0];
      push(@$long,$seqQualPair);
      push(@$sff,$file);
    }
    elsif(is_fasta($file,$settings)){
      my $qualfile="";
      $qualfile="$file.qual" if(-e "$file.qual");
      $seqQualPair=[$file,$qualfile];
      push(@$long,$seqQualPair);
    }
    elsif($$settings{is_illumina}{$file} || is_fastq($file,$settings) || is_fastqGz($file,$settings)){
      my $singletons;
      # quality trim fastq files
      ($file,$singletons)=qualityControlFastq($file,$settings);
      push(@$short,$file);
      push(@$short,$singletons) if(-s $singletons>0);
    }
    else{
      die "The format of $file is incompatible because its extension ".ext($file)." is not recognized.
      Try using --illumina or similar options to force a file type.";
    }
  }
  return {sff=>$sff,454=>$long,illumina=>$short};
}

# quality control the fastq reads.
sub qualityControlFastq{
  my($file,$settings)=@_;
  my $outfile="$$settings{tempdir}/".fileparse($file).".trimmed.fastq";
  my $singletons="$$settings{tempdir}/".fileparse($file).".trimmed.singletons.fastq";
  return ($outfile,$singletons) if(-s $outfile > 100); # skip this sub if the file already has some content
  
  # Trim 5' and 3' based on the stability of ATCG percentages.
  # This should remove any weird adapters/linkers/etc that are not really in the genome
  #my $trimmedFile=trimUpAndDown($file,$settings);

  # Downsample if it helps.
  # Pretend like this is the ony read set in terms of coverage.
  # TODO downsample later in the pipeline again or remove singletons if downsampling is needed
  if(my $e=$$settings{estimatedGenomeSize}){
    my $downsampled="$$settings{tempdir}/".fileparse($file).".downsampled.fastq.gz";
    my $targetCoverage=100;
    logmsg "Expected genome size was given as $e. Downsampling to target coverage of $targetCoverage";

    my $numBasesInReads=`run_assembly_readMetrics.pl '$file' | tail -n +2 | cut -f 3`+0;
    die "Could not determine the number of bases in $file" if(!$numBasesInReads);
    my $neededBases=$e*$targetCoverage;
    my $keepPercentage=$neededBases/$numBasesInReads;
    $keepPercentage=1.0 if($keepPercentage>1);
    logmsg "Total bases in reads were $numBasesInReads. Needed bases are only $neededBases. Therefore I am downsampling by $keepPercentage of the total reads";
    system("run_assembly_removeDuplicateReads.pl '$file' --downsample $keepPercentage | gzip -c > '$downsampled'");
    die if $?;
    $file=$downsampled;
  }

  # trimClean step
  logmsg "Performing a quality filter on the reads in $file ==> $outfile with CG-Pipeline's in-house method.";
  system("run_assembly_trimClean.pl --auto --numcpus=$$settings{numcpus} -i '$file' -o '$outfile' 2>&1");
  die if $?;
  return ($outfile,$singletons);
}

# Trim upstream and downstream based on the stability of ATCG percentages.
# Uses fastqqc internally
sub trimUpAndDown{
  my($file,$settings)=@_;
  logmsg "This is now deprecated";
  
  my $outfile="$$settings{tempdir}/".fileparse($file).".trimmed.fastq";
  system("run_assembly_trimLowQualEnds.pl '$file' > '$outfile'");
  die if $?;
  return $outfile;
}

# given a file containing one read accession per line and a pattern for all SFFs, extracts the reads and creates a new SFF
# the pattern for all SFFs is to be used in glob(), e.g. "sff/*.sff"
sub sffFastaQual($$$){
  my($acc,$sff,$settings)=@_;
  my $basename=$acc;
  $basename=~s/_acc$//;

  my $which=fileparse($basename); # newbler_outlier, newbler_singletons, etc

  my $command="sfffile -i '$acc' -o '$basename.sff' $sff";
  logmsg "COMMAND $command";
  system($command);
  die if $?;
  my $outlier_fastaqual = sff2fastaqual(["$$settings{tempdir}/$which.sff"], $settings);
  
  return $outlier_fastaqual;
}

# creates qual and sequence fasta files for an SFF file (basecalling)
sub sff2fastaqual($$) {
	my ($sff_files, $settings) = @_;
	my @fastaqualfiles;
	foreach my $input_file (@$sff_files) {
		my ($sff_file, $sff_dir) = fileparse($input_file);
		my $invoke_string = "sffinfo -seq '$sff_dir/$sff_file' > '$$settings{tempdir}/$sff_file.fasta'";
    if(!-e "$$settings{tempdir}/$sff_file.fasta"){
      logmsg "Running $invoke_string";
      system($invoke_string) if(!-e "$$settings{tempdir}/$sff_file.fasta"); die if $?;
    }
    if(!-e "$$settings{tempdir}/$sff_file.qual"){
      $invoke_string = "sffinfo -qual '$sff_dir/$sff_file' > '$$settings{tempdir}/$sff_file.qual'";
      logmsg "Running $invoke_string";
      system($invoke_string) if(!-e "$$settings{tempdir}/$sff_file.qual"); die if $?;
    }
		push(@fastaqualfiles, ["$$settings{tempdir}/$sff_file.fasta", "$$settings{tempdir}/$sff_file.qual"]);
	}
	return \@fastaqualfiles;
}

sub fastq2fastaqual($$){
  my($fastq_files,$settings)=@_;
  my @fastaqualfiles;
  foreach my $input_file (@$fastq_files) {
    my ($fastq_file, $fastq_dir)=fileparse($input_file);
    open(FASTQ,"<",$input_file) or die "Could not open $input_file for reading because $!";
    open(FASTA,">","$$settings{tempdir}/$fastq_file.fasta") or die "Could not open $$settings{tempdir}/$fastq_file.fasta for writing because $!";
    open(QUAL,">","$$settings{tempdir}/$fastq_file.qual") or die "Could not open $$settings{tempdir}/$fastq_file.qual for writing because $!";
    logmsg "Converting fastq $input_file to $$settings{tempdir}/$fastq_file.fasta and $fastq_file.qual";
    my $i=0;
    my $defline="";
    while(my $fLine=<FASTQ>){
      my $readLineNumber=$i%4;
      if($readLineNumber==0){
        $defline=$fLine;
        $defline=~s/^@//;
        $defline=~s/\s+$//;
      } elsif($readLineNumber==1){       # sequence
        print FASTA ">$defline\n$fLine";
      } elsif($readLineNumber==3){       # quality
        chomp($fLine);
        my @qual=split(//,$fLine);
        for (@qual){
          $_=ord($_)-33;  # TODO make a distinction b/n illumina and Sanger variants
        }
        print QUAL ">$defline\n".join(" ",@qual)."\n";
      }
      $i++;
    }
    close FASTQ;
  }
  return \@fastaqualfiles;
}

sub usage{
	"Usage: $0 input.sff [, input2.sff, ... inputN.fastq, ... inputM.fasta] [-o output.fasta]
    Input files can be sff, fastq, and fasta.  *.fasta.qual files are considered as the relevant quality files
    -R references.mfa to perform a reference assembly 
    -t tempdir to name a specific temporary directory
    -n numcpus to specify a number of threads, instead of all possible threads
    -e for an estimated genome size, in bp (for 4MB genome, use -e 4000000)
      This is used for downsampling and could be used for displaying more accurate assembly metrics
    Use these flags to specify exact chemistry types
      --illumina file.fastq, --454 file.sff
  "
}

